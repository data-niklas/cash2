Main = { ( LineComment | MultilineComment | NEWLINE)* ~ RootBlock ~ ( LineComment | MultilineComment | NEWLINE)* }
Block = { NewLines ~ Statement ~ (Divider ~ NewLines ~ Statement)* ~ ";"? ~ NewLines }
Divider = _{ NEWLINE | ";" | EOI }
NewLines = _{ NEWLINE* }

RootBlock = { Block }
WrappedBlock = _{ "{" ~ NewLines ~ Block ~ NewLines ~ "}"}
NonKeywordStatement = _{ WhileLoop | ForLoop | Conditional | Assignment | WrappedBlock | ("$" ~ Pipe) | Expr }
KeywordStatement = { (Return ~ NonKeywordStatement) | ((Break | Continue) ~ NonKeywordStatement?) }
Return = {"return"}
Break = {"break"}
Continue = {"continue"}
Statement = _{ NonKeywordStatement | KeywordStatement }
Assignment = { Ident ~ Indexing* ~ Infix? ~ "=" ~ Expr }
ForLoop = { "for" ~ Ident ~ "in" ~ Expr ~ WrappedBlock }
WhileLoop = { "while" ~ Expr ~ WrappedBlock }
Conditional = {If ~ NewLines ~ (Elif | NEWLINE+ )* ~ Else?}
If = { "if" ~ Expr ~ WrappedBlock }
Elif = { "elif" ~ Expr ~ WrappedBlock }
Else = { "else" ~ WrappedBlock }


Expr = { Prefix* ~ Primary ~ Postfix* ~ (Infix ~ Prefix* ~ Primary ~ Postfix*)* }
Primary = _{ Literal | Ident | Paren | Capture | WrappedBlock}
Pipe = { PipeCall ~ ( "|" ~ PipeCall)* }
PipeCall = { Ident ~ FunctionCall}
Capture = { "$" ~ Pipe }
Literal = { Function | Range | Float | Int | String | Bool | List | Dict }
Paren = _{ "(" ~ Expr ~ ")" }

Infix = { "<<" | ">>" | "**" | "+" | "-" | "*" | "/" | "%" | "!=" | "==" | "<=" | ">=" | "<" | ">" | "&" | "|" | "^" | "in" }
Prefix = { "+" | "-" | "!" | "await" | "async" }
Postfix = { FunctionCall | Indexing }

FunctionCall = { "(" ~ Arguments? ~ ")"}
Indexing = { ("[" ~ Expr ~ "]") | ("::" ~ Ident) }
Arguments = _{ NewLines ~ Expr ~ ( "," ~ NewLines ~ Expr)* ~ NewLines }

Function = { "(" ~ FunctionParams? ~ ")" ~ "->" ~ WrappedBlock }
FunctionParams = { FunctionParam ~ ("," ~ NewLines ~ FunctionParam)* }
FunctionParam = { Ident ~ OptionalMarker? }
OptionalMarker = _{ "=" ~ (Literal | Ident)}

Dict = { "{" ~ NewLines ~ Pairs?  ~ NewLines ~ "}" }
Pairs = _{ Pair ~ ("," ~ NewLines ~ Pair)* }
Pair = { (Ident | String) ~ ":" ~ Expr }

List = { "[" ~ Arguments? ~ "]"}
Int = @{ "0x" ~ HexInt | "0b" ~ BinInt | "0o" ~ OctInt  | DecInt }
DecInt = @{ ASCII_DIGIT ~ ( "_"* ~ ASCII_DIGIT)* }
HexInt = @{ ASCII_HEX_DIGIT ~ ( "_"* ~ ASCII_HEX_DIGIT)* }
OctInt = @{ ASCII_OCT_DIGIT ~ ( "_"* ~ ASCII_OCT_DIGIT)* }
BinInt = @{ ASCII_BIN_DIGIT ~ ( "_"* ~ ASCII_BIN_DIGIT)* }
Float = @{ DecInt? ~ (("." ~ DecInt ~ "_"* ~ ( "e" ~ DecInt)?) | "e" ~ DecInt) }
Bool = @{ "false" | "true" }
Ident = @{ (!Keyword ~ ASCII_ALPHA ~ ( ASCII_ALPHANUMERIC | "_")* ) | (("$" | Keyword) ~ ( ASCII_ALPHANUMERIC | "_")+ ) }
Range = { (Int | Ident) ~ ".." ~ (Int | Ident) }


String = ${ ("\"" ~ DoubleQuoteContent ~ "\"") | ("'" ~ SingleQuoteContent ~ "'")}
DoubleQuoteContent = _{ Home? ~ ( Interpolation | Escape | DoubleQuoteText)* }
SingleQuoteContent = _{ (Escape | SingleQuoteText)* }
Interpolation = { "${" ~ Expr ~ "}"}
Home = @{ "~" }
Escape = { "\\" ~ (("x" ~ ASCII_HEX_DIGIT{1,4}) | ANY) }
DoubleQuoteText = { (!("\"" | Interpolation | Escape) ~ ANY)+ }
SingleQuoteText = {  (!("\'" | Escape) ~ ANY)+}


Keyword = @{ "else" | "if" | "elif" | "while" | "for" | "in" | "return" | "break" | "continue" } 

LineComment = _{ "#" ~ (!NEWLINE ~ ANY)* }
MultilineComment = _{"/*" ~ (!"*/" ~ ANY)* ~ "*/"}
WHITESPACE = _{ " " | "\t" }
COMMENT = _{ MultilineComment | LineComment }